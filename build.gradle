buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.1'
    }
}
apply plugin: 'com.android.application'

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://jitpack.io" }
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.0'

    packagingOptions {
        exclude 'proguard-project.txt'
        exclude 'project.properties'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/DEPENDENCIES'
    }

    defaultConfig {
        applicationId "com.amaze.filemanager"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 55
        versionName "3.1.2 Beta 10"
    }

    signingConfigs {
        release
        config
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
    lintOptions {
        abortOnError false
    }

    sourceSets {
        main {
            /*manifest.srcFile 'src/main/AndroidManifest.xml'
            java.srcDirs = ['src', 'src/main/java', 'src/main/java/']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res', 'src/main/res', 'src/main/res/']*/
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    productFlavors {
        fdroid {
            buildConfigField "boolean", "IS_VERSION_FDROID", "true"
        }
        play {
            buildConfigField "boolean", "IS_VERSION_FDROID", "false"
            signingConfig signingConfigs.config
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.readystatesoftware.systembartint:systembartint:1.0.3'
    compile 'com.android.support:design:25.1.0'
    compile 'com.github.junrar:junrar:0.7'
    compile 'org.apache.commons:commons-compress:1.10'
    compile 'com.android.support:appcompat-v7:25.1.0'
    compile 'com.android.support:palette-v7:25.1.0'
    compile 'com.android.support:cardview-v7:25.1.0'
    // Google Play Services
    playCompile 'com.google.android.gms:play-services-plus:7.8.0'
    compile project(':libs:StickyHeadersRecyclerView')
    compile 'com.github.clans:fab:1.6.1'
    compile 'com.afollestad.material-dialogs:core:0.9.2.3'
    compile 'com.github.PhilJay:MPAndroidChart:v3.0.1'
    //FTP Server
//        compile 'org.apache.mina:mina-core:2.0.13'
    //    compile 'org.apache.ftpserver:ftpserver-core:1.0.6'
    //    compile 'org.apache.ftpserver:ftplet-api:1.0.6'

    // https://mvnrepository.com/artifact/org.apache.mina/mina-core
    compile group: 'org.apache.mina', name: 'mina-core', version: '2.0.13'

    // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'

    // https://mvnrepository.com/artifact/org.slf4j/slf4j-simple
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.21'

    //compile files('libs/ftplet-api-1.1.0-SNAPSHOT.jar')
    // https://mvnrepository.com/artifact/org.apache.ftpserver/ftplet-api
    compile group: 'org.apache.ftpserver', name: 'ftplet-api', version: '1.1.0'

    //compile files('libs/ftpserver-core-1.1.0-SNAPSHOT.jar')
    // https://mvnrepository.com/artifact/org.apache.ftpserver/ftpserver-core
    compile group: 'org.apache.ftpserver', name: 'ftpserver-core', version: '1.1.0'

    //compile 'com.jcraft:jsch:0.1.53'
    compile 'com.android.volley:volley:1.0.0'
    compile 'eu.chainfire:libsuperuser:1.0.0.+'
}

def Properties props = new Properties()
def propFile = new File('signing.properties')

if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null &&
            props.containsKey('STORE_FILE') &&
            props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') &&
            props.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'signing.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'signing.properties not found'
    android.buildTypes.release.signingConfig = null
}
